measurement : centralvalue uncertainty* units? bracketed? junk?
		| range units? bracketed? junk?

centralvalue : number
range : number _HYPHEN number

uncertainty : _PM number
		| _PM number bracketed -> named_uncertainty
		| _PM number undercomment -> named_uncertainty
		| _PM? upper lower -> upperlower_uncertainty
		| _PM? lower upper -> upperlower_uncertainty
		| _PM? upper lower bracketed -> named_upperlower_uncertainty
		| _PM? lower upper bracketed -> named_upperlower_uncertainty

_PM.2 : "\pm"

units : (unit|inverseunit)+
//units : unit+ | (unit inverseunit+)
//units : (unit|inverseunit|raisedunit)+
//	| "(" units+ ")" // Doesn't work? - needs to be a type of unit? (no 's')

//unit : UNIT
//inverseunit : _PER UNIT
//raisedunit : UNIT upper

inverseunit : _PER unit
unit : UNIT upper?

_PER.2 : "/" | "per"

upper : _CARAT number
lower : _UNDERSCORE number

_CARAT.2 : "^"
_UNDERSCORE.2 : "_"

undercomment : _UNDERSCORE OTHER

bracketed : BRACKETED
BRACKETED.2 : "(" /[^\(\)]+/ ")"

_HYPHEN.2 : "\u002D" | "\u2013" | "\u2014" | "\u2212"

number : NUMBER
NUMBER.2 : /[\+\-]?[0-9\.]+/

//UNIT.2 : (SIPREFIX? SIUNIT) | /\%/
//SIUNIT.2 : "m" | "pc" | "sec" | "s"
//SIPREFIX.2 : "n" | "\micro" | "m" | "k" | "M" | "G"

UNIT.2 : (UNITPREFIX? UNITNAME) | /\%/
UNITNAME.2 : <UNITNAMES>
UNITPREFIX.2 : <UNITPREFIXES>

LATEX.1 : /\\[A-Za-z]+/
%ignore LATEX

junk : OTHER+
OTHER.0 : /[^\s]+/

%import common.WS
%ignore WS
